/**
 * # Module loader
 */

// Compute boot file's directory based on the filename
__dirname: [ __filename dirname '/' + ] ;

/**
 * ## `import-resolve`
 * Resolve path relative to boot file
 */
__import-resolve: [ ( __dirname ) >> resolve ] ;

/**
 * ## `_public-dict`
 * returns the local dictionary, expect "private" words
 *
 * ( -> {array} )
 */
__public-dict: [ locals [ dup 0 @ '_' = [ drop ] [ dup rcl ] choose eval ] map object ] ;

/**
 * ## `_scoped-dict`
 * returns the scoped dictionary
 *
 * ( -> {array} )
 */
__scoped-dict: [ scoped [ dup rcl ] map object ] ;

/**
 * ## `create-module`
 * Creates a module from the locally defined words
 */
__create-module: [ __scoped-dict __public-dict rewrite ] ;

// Definition of `import-module`
// Loads and executes a file in a child, expands out defintions, and defines a module

/**
 * ## `import-module`
 * Loads and creates a module from a file
 */
__load-module: [ [ read eval __create-module send ] >> fork drop ] ;
__load-and-sto-module: [ dup __load-module swap btoa sto ] ;

import: [
  __import-resolve dup
  btoa rcl dup boolean
    [ ]
    [ drop dup __load-and-sto-module btoa rcl ]
    choose eval
] ;;

/**
 * ## `load`
 * Reads a file onto the stack as a string and evaluates the content
 */
load: [ read eval ] ;;

/**
 * ## `if-exists-load`
 * Loads a file if it exists
 */
if-exists-load: [ dup exists swap [ load ] >> [ ] choose eval ] ;;


