/**
 * # Module loader
 */

/**
 * ## `import-resolve`
 * Resolve path relative to file
 */
__import-resolve: [ ( prelude.__sys_path__ ) >> resolve ] ;

__include: [ read eval ] ;

/**
 * ## `load`
 * Reads a file onto the stack as a string and evaluates the content
 */
include: [ __import-resolve __include ] ;;

/**
 * ## `import-module`
 * Loads and creates a module from a file
 */
__module_hash: [ hex-hash '_' swap + ] ;
__load-module: [ [ dup [ ] >> '__filename' swap def __include ] >> module ] ;
__load-and-sto-module: [ dup __load-module swap __module_hash swap def ] ;;

import: [
  __import-resolve   
  dup __module_hash defined?
    [ __module_hash eval ]
    [ dup __load-and-sto-module __module_hash eval ]
    choose eval
] ;;

require: [
  import
  use
] ;

/**
 * ## `if-exists-load`
 * Loads a file if it exists
 */
if-exists-load: [ dup exists swap [ __include ] >> [ ] choose eval ] ;;
