/**
 * # Core defined words
 */

// Shuffle words
slip: [ q< eval q> ] ;      // [b] [a] -> b [a]
run: [ dup slip ] ;;        // [a] -> a [a]
dip: [ swap slip ] ;;       // [b] [a] -> a [b]
dipd: [ q< dip q> ] ;;      // [d] [c] [b] [a] -> [d] b [c] [a]
sip: [ over slip ] ;;       // [b] [a] -> [b] a [b]    (aka keep)
keep: [ over slip ] ;;      // [b] [a] -> [b] a [b]    (aka sip)
over: [ dupd swap ] ;;      // [b] [a] -> [b] [a] [b]  (aka peek)
dupd: [ q< dup q> ] ;;      // [b] [a] -> [b] [b] [a]  (aka w)
bi: [ sipd eval ] ;;        // [c] [b] [a] -> [c] b [c] a
sipd: [ q< sip q> ] ;;      // [b] [a] -> [b] a [b]    (aka sip)
slipd: [ q< slip q> ] ;;
do: [ dup dipd ] ;;

// math
neg: [ 0 swap - ] ;
sum: [ [ + ] reduce* ] ;

foreach: [ * eval ] ;

pop: [ 0 -1 slice ] ;
shift: [ 1 null slice ] ;

>: [ cmp 1 = ] ;
<: [ cmp -1 = ] ;
>=: [ < ~ ] ;
<=: [ > ~ ] ;
=<: [ > ~ ] ;
!=: [ = ~ ] ;
get: [ q< @ dup null? swap q> swap choose ] ;
zipwith: [ zipinto in ] ;
return: [ stack send ] ;
yield: [ return suspend ] ;
delay: [ [ sleep ] >> slip eval ] ;

branch: [ choose eval ] ;
runn: [ [ -- dup -1 > ] [ [ run ] dip ] while drop ] ;

// aggregates
unit: [ [ ] >> ] ;
cons: [ >> ] ;
pair: [ unit >> ] ;
car: [ 0 @ ] ;
cdr: [ shift ] ;
first: [ 0 @ ] ;
last: [ -1 @ ] ;
rest: [ shift ] ;
head: [ 0 swap slice ] ;
tail: [ neg null slice ] ;
startswith: [ dup length [ head ] >> dip = ] ;
endswith: [ dup length [ tail ] >> dip = ] ;
contains: [ indexof 1 > ] ;
pad-left: [ over * [ swap + ] >> dip tail ] ;
lreverse: [ [ unit ] [ swap + ] map-reduce ] ;
sreverse: [ lreverse sum ] ;
reverse: [ dup string? sreverse: lreverse: branch ] ;;

// functional
chain: [ over nothing? ~ swap when ] ;
fmap: [ eval ] ;
reduce*: [ [ [ first ] [ rest ] bi ] dip foreach ] ;
map-reduce: [ [ map ] dip reduce* ] ;
reduce: [ [ * ] >> dip swap eval ] ;
filter: [ [ keepif ] >> map ] ;

// incrementors
--: [ 1 - ] ;
++: [ 1 + ] ;
pred: [ dup -- ] ;
succ: [ dup ++ ] ;

// control
times: [ runn drop ] ;
ifte: [ slipd branch ] ;
if: [ slip when ] ;
when: [ [ ] branch ] ;
guard: [ [ keep ] >> dip branch ] ;
orelse: [ [ null? ~ ] guard ] ;
keepif: [ over unit if ] ;
loop: [ [ eval ] keep 'loop' >> when ] ;
while: [ swap do + [ loop ] >> when ] ;
until: [ [ [ ~ ] + ] dip while ] ;
forever: [ [ eval ] keep forever: >> eval ] ;

appl: [ >> in ] ;
y: [ [ dup >> ] swap + dup >> ] ;

// types
types: [
    'number'
    'string'
    'boolean'
    'array'
    'date'
    'object'
    'null'
    'undefined'
    'symbol'
    'complex'
    'action'
    'future' ] ;

null?: [ null = ] ;
action?: [ type 'action' = ] ;
string?: [ type 'string' = ] ;
boolean?: [ type 'boolean' = ] ;
array?: [ type 'array' = ] ;

/**
 * ## `dict`
 * returns the local dictionary
 *
 * ( -> {array} )
 */
dict: [ locals [ dup rcl ] map object ] ;
