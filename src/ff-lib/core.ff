/**
 * # Core defined words
 */
'types.ff' import use

// core shuffle words
slip: [ q< eval q> ] ;      // [b] A -> b A
run: [ dup slip ] ;;        // [a] -> a [a]
dip: [ swap slip ] ;;       // B [a] -> a B
sip: [ over slip ] ;;       // B [a] -> B a B    (aka keep)
dive: [ swap dipd ] ;       // [c] [b] [a] -> a [c] [b]   (aka dip2)

// diped
dipd: [ q< dip q> ] ;;      // D C [b] A -> D b C A
sipd: [ q< sip q> ] ;;      // C B [a] -> C a C B
dupd: [ q< dup q> ] ;       // B A -> B B A
slipd: [ q< slip q> ] ;;    // [c] B A -> c B A
over: [ dupd swap ] ;;      // B A -> B A B  (aka peek)
swapd: [ q< swap q> ] ;     // C B A -> B C A

bi: [ sipd eval ] ;;        // C [b] [a] -> C b C a
do: [ dup dipd ] ;;

// keep/check
keep: [ over slip ] ;;      // B [a] -> B a B    (aka sip)
check: [ dupd eval ] ;;     // B [a] -> B B a    (aka w)

// truthy/falsy
truthy?: [ boolean ] ;
falsy?: [ truthy? ~ ] ;;

// Comparisons
>: [ <=> 1 = ] ;
<: [ <=> -1 = ] ;
>=: [ < ~ ] ;;
<=: [ > ~ ] ;;
=<: [ > ~ ] ;;
!=: [ = ~ ] ;

max: [ dup2 q< q< > q> q> choose ] ;; // aka greater-of
min: [ dup2 q< q< < q> q> choose ] ;; // lesser-of

// 
get: [ q< @ dup null? swap q> swap choose ] ;
zipwith: [ zipinto in ] ;
return: [ stack send ] ;
yield: [ return suspend ] ;
delay: [ [ sleep ] >> slip eval ] ;

// control
_check: [ [ truthy? ] dive ] ;
branch: [ _check choose eval ] ;
when: [ [ ] branch ] ;
unless: [ [ ] swap branch ] ;
ifte: [ slipd branch ] ;;
if: [ slip when ] ;
while: [ swap do + [ loop ] >> when ] ;

// folds
each: [ * eval ] ;
reverse-each: [ / eval ] ;
foldl: [ swapd each ] ;;
foldr: [ [ swap ] swap + swapd reverse-each ] ;;
foldl1: [ [ uncons ] dip each ] ;;
foldr1: [ [ uncons ] dip reverse-each ] ;;
fold: [ foldl1 ] ;;
runn: [ [ -- dup -1 > ] [ [ run ] dip ] while drop ] ;
times: [ runn drop ] ;
guard: [ [ keep ] >> dip branch ] ;
orelse: [ [ null? ~ ] guard ] ;
keepif: [ over unit if ] ;
loop: [ [ eval ] keep [ loop ] >> when ] ;
until: [ [ [ ~ ] + ] dip while ] ;
forever: [ [ eval ] keep forever: >> eval ] ;
either?: [ bi + ] ;

appl: [ >> in ] ;
y: [ [ dup >> ] swap + dup >> ] ;

// lists
unit: [ [ ] >> ] ;
cons: [ >> ] ;
pair: [ unit >> ] ;

// aggregates
slice: [ [ % ] dip \ ] ;
pop: [ -1 \ ] ;
shift: [ 1 % ] ;
car: [ 0 @ ] ;
cdr: [ 1 % ] ;
first: [ 0 @ ] ;
last: [ -1 @ ] ;
rest: [ 1 % ] ;
head: [ \ ] ;
tail: [ % ] ;
uncons: [ [ first ] [ rest ] bi ] ;;
startswith: [ dup ln [ head ] >> dip = ] ;;
endswith: [ dup ln [ tail ] >> dip = ] ;;
contains: [ indexof 1 > ] ;

// functional
chain: [ over nothing? ~ swap when ] ;
fmap: [ eval ] ;
map-reduce: [ [ map ] dip fold ] ;
reduce: [ foldl ] ;;
filter: [ [ dup empty swap ] dip [ keepif ] >> map swap * ] ;

// aggregate reduction
maximum: [ [ max ] fold ] ;;
minimum: [ [ min ] fold ] ;;
reverse: [ dup ln 1 > [ uncons reverse swap + ] when ] ;

flatten*: [
  dup array?
  [ [ flatten* ] foreach ]
  when
] ;
flatten: [ [ flatten* ] appl ] ;

// math
neg: [ 0 swap - ] ;
sum: [ [ + ] fold ] ;

// incrementors
--: [ 1 - ] ;
++: [ 1 + ] ;
pred: [ dup -- ] ;
succ: [ dup ++ ] ;

/**
 * ## `dict`
 * returns the local dictionary
 *
 * ( -> {array} )
 */
dict: [ locals [ dup rcl ] map object ] ;

/**
  * ## `nop`
  * no op
  *
  * ( -> )
  */
nop: [] ;

/**
 * Calls the second quotation in the first pair whose first quotation yields a truthy value.
 */
cond: [
  dup empty?
  [
    uncons slip slipd
    [ cond ] >>
    branch
  ] unless
] ;

/**
 * Calls the second quotation in the first pair whose first quotation matchs the last item on the stack
 */
p-cond: [
  dup empty?
  [
    uncons [ eval [ dupd =~ ] dip ] dip
    [ p-cond ] >>
    branch
  ] unless
] ;

/**
 * Returns the second item in the first pair whose first quotation matches the last item on the stack
 */
p-choose: [
  dup empty?
  [
    uncons [ eval [ dupd =~ ] dip [ drop ] swap << ] dip
    [ p-choose ] >>
    branch
  ] unless
] ;
