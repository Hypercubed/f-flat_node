/**
 * # Module loader
 */

// Resolve path relative to file
__import-resolve: [ ( prelude.__sys_path__ ) >> resolve ] def

/**
 * ## `include`
 * Reads a file onto the stack as a string and evaluates the content
 */
 __include: [ read eval ] def
include: [ __import-resolve __include ] def

/**
 * ## `export`
 * Exports the current vocab and sends to parent
 */
export: [ vocab send ] def

__module_hash: [ hex-hash '%top._' swap + ] def
__load-module: [ [ dup [ ] >> '__filename' swap def __include export ] >> fork drop ] def
__load-and-sto-module: [ dup __load-module swap __module_hash swap def ] def

/**
 * ## `import`
 * Loads and creates a module from a file
 */
import: [
  __import-resolve   
  dup __module_hash defined?
    [ __module_hash eval ]
    [ dup __load-and-sto-module __module_hash eval ]
    choose eval
] def

/**
 * ## `require`
 * Imports and uses a module from a file
 */
require: [
  import
  use
] def

/**
 * ## `if-exists-load`
 * Loads a file if it exists
 */
if-exists-load: [ dup exists swap [ __include ] >> [ ] choose eval ] def
