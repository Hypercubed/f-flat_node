/**
 * # Mathematics lib
 * Many words derived from (Joy)[http://tunes.org/~iepos/joy.html] and Factor(http://factorcode.org/).
 */

'core.ff' import use

drop2: [ drop drop ] ;        // B A ->

m: [ dup eval ] ;             // [a] -> [a] a

dup2: [ over over ] ;         // B A -> B A B A

keep2: [ q< dup2 q> dive ] ;  // [c] [b] [a] -> [c] [b] a [c] [b]   (aka 2keep)

nip: [ swap drop ] ;          // B [a] -> [a]
dropd: [ q< drop q> ] ;       // B [a] -> [a]          (aka nip)
tuck: [ swap over ] ;         // [b] [a] -> [a] [b] [a]

overd: [ q< over q> ] ;       // [c] [b] [a] -> [c] [b] [c] [a]
pick: [ overd swap ] ;        // [c] [b] [a] -> [c] [b] [a] [c]
dup3: [ pick pick pick ] ;    // [c] [b] [a] -> [c] [b] [a] [c] [b] [a]
swapd: [ q< swap q> ] ;       // [c] [b] [a] -> [b] [c] [a]
dig: [ swapd swap ] ;         // [c] [b] [a] -> [b] [a] [c]
rolldown: [ swapd swap ] ;    // [c] [b] [a] -> [b] [a] [c] (aka dig)
bury: [ swap swapd ] ;        // [c] [b] [a] -> [a] [c] [b]
flip: [ swapd bury ] ;        // [c] [b] [a] -> [a] [b] [c]
rot: [ bury swap ] ;          // [c] [b] [a] -> [a] [b] [c]

dip2: [ swap dipd ] ;            // [c] [b] [a] -> a [c] [b]

bi2: [ q< keep2 q> eval ] ;;     // D C [b] [a] -> D C b D C a
bi*: [ dipd eval ] ;;            // D C [b] [a] -> D b C a
bi@: [ dup bi* ] ;;              // C B [a] -> C a B a
tri: [ q< sipd sip q> eval ] ;;  // D [c] [b] [a] -> D c D b D a


